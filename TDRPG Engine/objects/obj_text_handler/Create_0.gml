defaultTypeSpeed = game_get_speed(gamespeed_fps) / 30;
defaultNewlineStr = "";
defaultTextTransform = undefined;
defaultTextRender = undefined;
textTransformStruct = {char: "A", x: 0, y: 0, xscale: 1, yscale: 1, angle: 0};

defaultTypeSound = undefined;
playTypeSound = false;

text = "";
progress = 0;

frame = 0;

set_text = function(str = "") {
	newlineStr = defaultNewlineStr;
	text = newlineStr + str;
	
	commandArray = [];
	paramArray = [];
	
	for (var i=1; i<=string_length(text); i++) {
		var char = string_char_at(text, i);
		
		if (char = "\n") {
			text = string_insert(newlineStr, text, i + 1);
			i += string_length(newlineStr);
			continue;
		}
		
		if (char = "\a") { //can't have any of these in there
			text = string_delete(text, i, 1);
			i --;
			continue;
		}
		
		if (char != "{") continue;
		
		if (string_char_at(text, i + 1) = "{") {
			text = string_delete(text, i, 1);
			continue;
		}
		
		//get command and parameters
		var endPos = string_pos_ext("}", text, i + 1);
		if (endPos = 0) break; //malformed command
		var params = string_copy(text, i + 1, endPos - i - 1);
		params = string_split(params, " ", true);
		if (array_length(params) <= 0) continue; //malformed command
		var commandName = params[0];
		var command = global.__text_data.command[$ commandName];
		array_delete(params, 0, 1);
		
		if (!is_struct(command)) { //command does not exist
			i = endPos;
			continue;
		}
		
		var init = command[$ "init"];
		if is_method(init) init(id, params);
		
		var newStr = "";
		
		if (!command.removable) { //command has types that run post-init
			var index = array_length(commandArray);
			commandArray[index] = command;
			paramArray[index] = params;
			newStr += "\a" + string_format(index, 2, 0);
		}
		
		var variable = command[$ "variable"];
		if (variable != undefined) {
			if is_method(variable) {
				newStr += string(variable(id, params));
			} else {
				newStr += string(variable);
			}
		}
		
		text = string_delete(text, i, endPos - i + 1);
		text = string_insert(newStr, text, i);
		i += string_length(newStr) - 1;
	}
	
	typeTimer = 1;
	typeSpeed = defaultTypeSpeed;
	typeSound = defaultTypeSound;
	progress = 0;
	progressMax = string_length(text);
	
	newlinePos = [infinity];
	lineWidth = [0];
	lineCount = 0;
	
	prevWidth = undefined;
	prevFont = undefined;
}

set_newline_str = function(str = "") {
	defaultNewlineStr = str;
}

set_effect = function(effect_name) {
	var command = global.__text_data.command[$ effect_name];
	if (command = undefined) {
		defaultTextTransform = undefined;
		defaultTextRender = undefined;
		return;
	}
	defaultTextTransform = command[$ "transform"];
	defaultTextRender = command[$ "render"];
}

set_sound = function(sound) {
	if (!is_handle(sound) && !is_array(sound)) sound = undefined;
	defaultTypeSound = sound;
	typeSound = sound;
}

set_speed = function(spd) {
	defaultTypeSpeed = game_get_speed(gamespeed_fps) / spd;
	typeSpeed = defaultTypeSpeed;
}

skip = function() {
	progress = progressMax;
}

is_finished = function() {
	return (progress >= progressMax);
}

draw = function(_x, _y, sep = -1, w = -1, xscale = 1, yscale = 1, angle = 0) {
	var font = draw_get_font();
	w = (w >= 0) ? w : infinity;
	
	if (prevFont != font || prevWidth != w) {
		prevFont = font;
		prevWidth = w;
		refresh(w); //update word wrap
	}
	
	#region set values
	defaultColor = draw_get_color();
	defaultFont = font;
	defaultHalign = draw_get_halign();
	switch defaultHalign {
		default: halign = 0; break;
		case fa_center: halign = -0.5; break;
		case fa_right: halign = -1; break;
	}
	draw_set_halign(fa_left);
	defaultValign = draw_get_valign();
	draw_set_valign(fa_top);
	
	textTransform = defaultTextTransform;
	textRender = defaultTextRender;
	var newlineX = string_width(newlineStr);
	lineHeight = (sep >= 0) ? sep : string_height("M");
	drawXScale = xscale;
	drawYScale = yscale;
	drawAngle = angle;
	drawX = _x;
	drawY = _y;
	xx = halign * lineWidth[0];
	yy = 0;
	var lineIndex = 0;
	var wrapPos = newlinePos[0];
	#endregion
	
	for (var i=1; i<=progress; i++) {
		var char = string_char_at(text, i);
		
		if (i >= wrapPos) { //go to new line
			lineIndex ++;
			wrapPos = newlinePos[lineIndex];
			xx = halign * lineWidth[lineIndex];
			yy += lineHeight;
			
			if (i > 1) {
				if (string_char_at(text, i - 1) = "\n") {
					newlineX = string_width(newlineStr);
				} else {
					xx += newlineX;
				}
			}
		}
		
		if (char = "\a") {
			var index = real(string_copy(text, i + 1, 2));
			var command = commandArray[index];
			var func = command[$ "draw"];
			if is_method(func) func(id, paramArray[index]);
			textTransform = command[$ "transform"] ?? textTransform;
			if (textTransform = "reset") textTransform = defaultTextTransform;
			textRender = command[$ "render"] ?? textRender;
			if (textRender = "reset") textRender = defaultTextRender;
			i += 2;
		} else {
			#region transform text
			var trans = textTransformStruct;
			trans.char = char;
			trans.x = xx;
			trans.y = yy;
			trans.xscale = 1;
			trans.yscale = 1;
			trans.angle = 0;
			
			if (textTransform != undefined) {
				if is_method(textTransform) {
					textTransform(id, trans);
				} else switch textTransform {
				case "shake":
					trans.x += irandom_range(-1, 1);
					trans.y += irandom_range(-1, 1);
					break;
				case "wave":
					trans.y += sin(trans.x * 0.1 - current_time * 0.005) * 2;
					break;
				case "scared":
					if (irandom(200) = 0) {
						trans.y += irandom(1) * 2 - 1;
					}
					break;
				}
			}
			
			if (drawAngle != 0) {
				var tempX = trans.x * drawXScale;
				var tempY = trans.y * drawYScale;
				var sinA = dsin(drawAngle);
				var cosA = dcos(drawAngle);
				trans.x = drawX + cosA * tempX + sinA * tempY;
				trans.y = drawY + cosA * tempY - sinA * tempX;
				trans.angle += drawAngle;
			} else {
				trans.x = drawX + trans.x * drawXScale;
				trans.y = drawY + trans.y * drawYScale;
			}
			trans.xscale *= drawXScale;
			trans.yscale *= drawYScale;
			#endregion
			
			if is_method(textRender) {
				textRender(id, trans);
			} else {
				draw_text_transformed(trans.x, trans.y, trans.char, trans.xscale, trans.yscale, trans.angle);
			}
			xx += string_width(char);
		}
	}
	
	draw_set_color(defaultColor);
	draw_set_font(defaultFont);
	draw_set_halign(defaultHalign);
	draw_set_valign(defaultValign);
}

refresh = function(w) {
	defaultFont = draw_get_font();
	var maxWidth = w;
	var newlineX = string_width(newlineStr);
	var xx = 0;
	var width = 0;
	wordWidth = 0;
	var wrapPos = 0;
	var lineIndex = 0;
	var str = text + "\n";
	
	for (var i=1; i<=progressMax+1; i++) {
		var char = string_char_at(str, i);
		
		if (char = " " || char = "\n") {
			if (wordWidth > 0) {
				if (xx + wordWidth > maxWidth) {
					lineWidth[lineIndex] = width;
					newlinePos[lineIndex] = wrapPos;
					lineIndex ++;
					xx = newlineX;
				}
				
				xx += wordWidth;
				width = xx;
				wordWidth = 0;
			}
			
			wrapPos = i + 1;
			
			if (char = "\n") {
				lineWidth[lineIndex] = width;
				newlinePos[lineIndex] = wrapPos;
				lineIndex ++;
				newlineX = string_width(newlineStr);
				xx = 0;
				width = 0;
				wordWidth = 0;
			} else {
				xx += string_width(char);
			}
		} else if (char = "\a") {
			var index = real(string_copy(text, i + 1, 2));
			var command = commandArray[index];
			var func = command[$ "refresh"];
			if is_method(func) func(id, paramArray[index]);
			i += 2;
		} else {
			wordWidth += string_width(char);
		}
	}
	
	lineCount = lineIndex;
	draw_set_font(defaultFont);
}

#region in text commands API
play_type_sound = function() {
	playTypeSound = true;
}

wait = function(time = 1) {
	typeTimer += typeSpeed * time;
}

wait_seconds = function(seconds) {
	typeTimer += game_get_speed(gamespeed_fps) * seconds;
}
#endregion